
// Ported by Isaac Shelton from Erin Catto's Box2D @ 95f74a46c077825478f989df6a81003ca538c2c2

// MIT License

// Copyright (c) 2020-2021 Isaac Shelton

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

/*
Position Correction Notes
=========================
I tried the several algorithms for position correction of the 2D revolute joint.
I looked at these systems:
- simple pendulum (1m diameter sphere on massless 5m stick) with initial angular velocity of 100 rad/s.
- suspension bridge with 30 1m long planks of length 1m.
- multi-link chain with 30 1m long links.
Here are the algorithms:
Baumgarte - A fraction of the position error is added to the velocity error. There is no
separate position solver.
Pseudo Velocities - After the velocity solver and position integration,
the position error, Jacobian, and effective mass are recomputed. Then
the velocity constraints are solved with pseudo velocities and a fraction
of the position error is added to the pseudo velocity error. The pseudo
velocities are initialized to zero and there is no warm-starting. After
the position solver, the pseudo velocities are added to the positions.
This is also called the First Order World method or the Position LCP method.
Modified Nonlinear Gauss-Seidel (NGS) - Like Pseudo Velocities except the
position error is re-computed for each constraint and the positions are updated
after the constraint is solved. The radius vectors (aka Jacobians) are
re-computed too (otherwise the algorithm has horrible instability). The pseudo
velocity states are not needed because they are effectively zero at the beginning
of each iteration. Since we have the current position error, we allow the
iterations to terminate early if the error becomes smaller than b2_linearSlop.
Full NGS or just NGS - Like Modified NGS except the effective mass are re-computed
each time a constraint is solved.
Here are the results:
Baumgarte - this is the cheapest algorithm but it has some stability problems,
especially with the bridge. The chain links separate easily close to the root
and they jitter as they struggle to pull together. This is one of the most common
methods in the field. The big drawback is that the position correction artificially
affects the momentum, thus leading to instabilities and false bounce. I used a
bias factor of 0.2. A larger bias factor makes the bridge less stable, a smaller
factor makes joints and contacts more spongy.
Pseudo Velocities - the is more stable than the Baumgarte method. The bridge is
stable. However, joints still separate with large angular velocities. Drag the
simple pendulum in a circle quickly and the joint will separate. The chain separates
easily and does not recover. I used a bias factor of 0.2. A larger value lead to
the bridge collapsing when a heavy cube drops on it.
Modified NGS - this algorithm is better in some ways than Baumgarte and Pseudo
Velocities, but in other ways it is worse. The bridge and chain are much more
stable, but the simple pendulum goes unstable at high angular velocities.
Full NGS - stable in all tests. The joints display good stiffness. The bridge
still sags, but this is better than infinite forces.
Recommendations
Pseudo Velocities are not really worthwhile because the bridge and chain cannot
recover from joint separation. In other cases the benefit over Baumgarte is small.
Modified NGS is not a robust method for the revolute joint due to the violent
instability seen in the simple pendulum. Perhaps it is viable with other constraint
types, especially scalar constraints where the effective mass is a scalar.
This leaves Baumgarte and Full NGS. Baumgarte has small, but manageable instabilities
and is very fast. I don't think we can escape Baumgarte, especially in highly
demanding cases where high constraint fidelity is not needed.
Full NGS is robust and easy on the eyes. I recommend this as an option for
higher fidelity simulation and certainly for suspension bridges and long chains.
Full NGS might be a good choice for ragdolls, especially motorized ragdolls where
joint separation can be problematic. The number of NGS iterations can be reduced
for better performance without harming robustness much.
Each joint in a can be handled differently in the position solver. So I recommend
a system where the user can select the algorithm on a per joint basis. I would
probably default to the slower Full NGS and let the user select the faster
Baumgarte method in performance critical scenarios.
*/

/*
Cache Performance
The Box2D solvers are dominated by cache misses. Data structures are designed
to increase the number of cache hits. Much of misses are due to random access
to body data. The constraint structures are iterated over linearly, which leads
to few cache misses.
The bodies are not accessed during iteration. Instead read only data, such as
the mass values are stored with the constraints. The mutable data are the constraint
impulses and the bodies velocities/positions. The impulses are held inside the
constraint structures. The body velocities/positions are held in compact, temporary
arrays to increase the number of cache hits. Linear and angular velocity are
stored in a single array since multiple arrays lead to multiple misses.
*/

/*
2D Rotation
R = [cos(theta) -sin(theta)]
    [sin(theta) cos(theta) ]
thetaDot = omega
Let q1 = cos(theta), q2 = sin(theta).
R = [q1 -q2]
    [q2  q1]
q1Dot = -thetaDot * q2
q2Dot = thetaDot * q1
q1_new = q1_old - dt * w * q2
q2_new = q2_old + dt * w * q1
then normalize.
This might be faster than computing sin+cos.
However, we can compute sin+cos of the same angle fast.
*/

import "b2_body.adept"
import "b2_math.adept"
import "b2_time_step.adept"

import "b2_contact.adept"
import "b2_distance.adept"
import "b2_fixture.adept"
import "b2_joint.adept"
import "b2_stack_allocator.adept"
import "b2_timer.adept"
import "b2_world.adept"
import "b2_contact_solver.adept"

/*
REQUIRES:
    class b2Contact;
    class b2Joint;
    class b2StackAllocator;
    class b2ContactListener;
    struct b2ContactVelocityConstraint;
    struct b2Profile;
*/

/// This is an internal class.
struct b2Island (
    m_allocator *b2StackAllocator,
    m_listener *b2ContactListener,

    m_bodies **b2Body,
    m_contacts **b2Contact,
    m_joints **b2Joint,

    m_positions *b2Position,
    m_velocities *b2Velocity,

    m_bodyCount int,
    m_jointCount int,
    m_contactCount int,

    m_bodyCapacity int,
    m_contactCapacity int,
    m_jointCapacity int
) {
    func __defer__ {
        // Warning: the order should reverse the constructor order.
        this.m_allocator.Free(this.m_positions)
        this.m_allocator.Free(this.m_velocities)
        this.m_allocator.Free(this.m_joints)
        this.m_allocator.Free(this.m_contacts)
        this.m_allocator.Free(this.m_bodies)
    }

    func Clear() void {
        this.m_bodyCount = 0
        this.m_contactCount = 0
        this.m_jointCount = 0
    }

    func Solve(profile *b2Profile, in step *b2TimeStep, in gravity *b2Vec2, allowSleep bool) void {
        timer POD b2Timer = b2Timer()

        h float = step.dt

        // Integrate velocities and apply damping. Initialize the body state.
        for(i int = 0; i < this.m_bodyCount; ++i){
            b *b2Body = this.m_bodies[i]

            c POD b2Vec2 = b.m_sweep.c
            a float = b.m_sweep.a
            v POD b2Vec2 = b.m_linearVelocity
            w float = b.m_angularVelocity

            // Store positions for continuous collision.
            b.m_sweep.c0 = b.m_sweep.c
            b.m_sweep.a0 = b.m_sweep.a

            if b.m_type == b2_dynamicBody {
                // Integrate velocities.
                v.AddAssign(h * b.m_invMass * (b.m_gravityScale * b.m_mass * *gravity + b.m_force))
                w += h * b.m_invI * b.m_torque

                // Apply damping.
                // ODE: dv/dt + c * v = 0
                // Solution: v(t) = v0 * exp(-c * t)
                // Time step: v(t + dt) = v0 * exp(-c * (t + dt)) = v0 * exp(-c * t) * exp(-c * dt) = v * exp(-c * dt)
                // v2 = exp(-c * dt) * v1
                // Pade approximation:
                // v2 = v1 * 1 / (1 + c * dt)
                v = v * (1.0f / (1.0f + h * b.m_linearDamping))
                w *= 1.0f / (1.0f + h * b.m_angularDamping)
            }

            this.m_positions[i].c = c
            this.m_positions[i].a = a
            this.m_velocities[i].v = v
            this.m_velocities[i].w = w
        }

        timer.Reset()

        // Solver data
        solverData POD b2SolverData = undef
        solverData.step = *step
        solverData.positions = this.m_positions
        solverData.velocities = this.m_velocities

        // Initialize velocity constraints.
        contactSolverDef POD b2ContactSolverDef = undef
        contactSolverDef.step = *step
        contactSolverDef.contacts = this.m_contacts
        contactSolverDef.count = this.m_contactCount
        contactSolverDef.positions = this.m_positions
        contactSolverDef.velocities = this.m_velocities
        contactSolverDef.allocator = this.m_allocator

        contactSolver b2ContactSolver = undef
        b2ContactSolver(&contactSolver, &contactSolverDef)
        contactSolver.InitializeVelocityConstraints();

        if step.warmStarting {
            contactSolver.WarmStart()
        }
        
        for(i int = 0; i < this.m_jointCount; ++i){
            this.m_joints[i].InitVelocityConstraints(&solverData)
        }

        profile.solveInit = timer.GetMilliseconds()

        // Solve velocity constraints
        timer.Reset()
        for(i int = 0; i < step.velocityIterations; ++i){
            for(j int = 0; j < this.m_jointCount; ++j){
                this.m_joints[j].SolveVelocityConstraints(&solverData)
            }

            contactSolver.SolveVelocityConstraints()
        }

        // Store impulses for warm starting
        contactSolver.StoreImpulses()
        profile.solveVelocity = timer.GetMilliseconds()

        // Integrate positions
        for(i int = 0; i < this.m_bodyCount; ++i){
            c POD b2Vec2 = this.m_positions[i].c
            a float = this.m_positions[i].a
            v POD b2Vec2 = this.m_velocities[i].v
            w float = this.m_velocities[i].w

            // Check for large velocities
            translation POD b2Vec2 = h * v
            if b2Dot(translation, translation) > b2_maxTranslationSquared {
                ratio float = b2_maxTranslation / translation.Length()
                v.MultiplyAssign(ratio)
            }

            rotation float = h * w
            if rotation * rotation > b2_maxRotationSquared {
                ratio float = b2_maxRotation / b2Abs(rotation)
                w *= ratio
            }

            // Integrate
            c.AddAssign(h * v)
            a += h * w

            this.m_positions[i].c = c
            this.m_positions[i].a = a
            this.m_velocities[i].v = v
            this.m_velocities[i].w = w
        }

        // Solve position constraints
        timer.Reset()
        positionSolved bool = false
        for(i int = 0; i < step.positionIterations; ++i){
            contactsOkay bool = contactSolver.SolvePositionConstraints()

            jointsOkay bool = true
            for(j int = 0; j < this.m_jointCount; ++j){
                jointOkay bool = this.m_joints[j].SolvePositionConstraints(&solverData)
                jointsOkay = jointsOkay && jointOkay
            }

            if contactsOkay && jointsOkay {
                // Exit early if the position errors are small.
                positionSolved = true
                break
            }
        }

        // Copy state buffers back to the bodies
        for(i int = 0; i < this.m_bodyCount; ++i){
            body *b2Body = this.m_bodies[i]
            body.m_sweep.c = this.m_positions[i].c
            body.m_sweep.a = this.m_positions[i].a
            body.m_linearVelocity = this.m_velocities[i].v
            body.m_angularVelocity = this.m_velocities[i].w
            body.SynchronizeTransform()
        }

        profile.solvePosition = timer.GetMilliseconds()

        this.Report(contactSolver.m_velocityConstraints)

        if allowSleep {
            minSleepTime float = b2_maxFloat

            const linTolSqr float = b2_linearSleepTolerance * b2_linearSleepTolerance
            const angTolSqr float = b2_angularSleepTolerance * b2_angularSleepTolerance

            for(i int = 0; i < this.m_bodyCount; ++i){
                b *b2Body = this.m_bodies[i]
                if b.GetType() == b2_staticBody, continue

                if ( (b.m_flags & b2Body\e_autoSleepFlag) == 0 || b.m_angularVelocity * b.m_angularVelocity > angTolSqr || b2Dot(b.m_linearVelocity, b.m_linearVelocity) > linTolSqr ){
                    b.m_sleepTime = 0.0f
                    minSleepTime = 0.0f
                } else {
                    b.m_sleepTime += h
                    minSleepTime = b2Min(minSleepTime, b.m_sleepTime)
                }
            }

            if minSleepTime >= b2_timeToSleep && positionSolved {
                for(i int = 0; i < this.m_bodyCount; ++i) {
                    b *b2Body = this.m_bodies[i]
                    b.SetAwake(false)
                }
            }
        }
    }

    func SolveTOI(in subStep *b2TimeStep, toiIndexA, toiIndexB int) void {
        b2Assert(toiIndexA < this.m_bodyCount, #get __file__, #get __line__, 'b2Assert(toiIndexA < this.m_bodyCount)')
        b2Assert(toiIndexB < this.m_bodyCount, #get __file__, #get __line__, 'b2Assert(toiIndexB < this.m_bodyCount)')

        // Initialize the body state.
        for(i int = 0; i < this.m_bodyCount; ++i){
            b *b2Body = this.m_bodies[i]
            this.m_positions[i].c = b.m_sweep.c
            this.m_positions[i].a = b.m_sweep.a
            this.m_velocities[i].v = b.m_linearVelocity
            this.m_velocities[i].w = b.m_angularVelocity
        }

        contactSolverDef POD b2ContactSolverDef = undef
        contactSolverDef.contacts = this.m_contacts
        contactSolverDef.count = this.m_contactCount
        contactSolverDef.allocator = this.m_allocator
        contactSolverDef.step = *subStep
        contactSolverDef.positions = this.m_positions
        contactSolverDef.velocities = this.m_velocities

        contactSolver b2ContactSolver = undef
        b2ContactSolver(&contactSolver, &contactSolverDef)

        // Solve position constraints.
        for(i int = 0; i < subStep.positionIterations; ++i){
            contactsOkay bool = contactSolver.SolveTOIPositionConstraints(toiIndexA, toiIndexB)
            if contactsOkay, break
        }

        // UNUSED CODE
        /*
        #if false
        // Is the new position really safe?
        for(i int = 0; i < this.m_contactCount; ++i){
            c *b2Contact = this.m_contacts[i]
            fA *b2Fixture = c.GetFixtureA()
            fB *b2Fixture = c.GetFixtureB()

            bA *b2Body = fA.GetBody()
            bB *b2Body = fB.GetBody()

            indexA int = c.GetChildIndexA()
            indexB int = c.GetChildIndexB()

            input b2DistanceInput
            input.proxyA.Set(fA.GetShape(), indexA)
            input.proxyB.Set(fB.GetShape(), indexB)
            input.transformA = bA.GetTransform()
            input.transformB = bB.GetTransform()
            input.useRadii = false

            output b2DistanceOutput
            cache b2SimplexCache
            cache.count = 0
            b2Distance(&output, &cache, &input)

            if output.distance == 0 || cache.count == 3 {
                cache.count += 0
            }
        }
        #end
        */

        // Leap of faith to new safe state.
        this.m_bodies[toiIndexA].m_sweep.c0 = this.m_positions[toiIndexA].c
        this.m_bodies[toiIndexA].m_sweep.a0 = this.m_positions[toiIndexA].a
        this.m_bodies[toiIndexB].m_sweep.c0 = this.m_positions[toiIndexB].c
        this.m_bodies[toiIndexB].m_sweep.a0 = this.m_positions[toiIndexB].a

        // No warm starting is needed for TOI events because warm
        // starting impulses were applied in the discrete solver.
        contactSolver.InitializeVelocityConstraints()

        // Solve velocity constraints.
        for(i int = 0; i < subStep.velocityIterations; ++i){
            contactSolver.SolveVelocityConstraints()
        }

        // Don't store the TOI contact forces for warm starting
        // because they can be quite large.
        h float = subStep.dt

        // Integrate positions
        for(i int = 0; i < this.m_bodyCount; ++i){
            c b2Vec2 = this.m_positions[i].c
            a float = this.m_positions[i].a
            v b2Vec2 = this.m_velocities[i].v
            w float = this.m_velocities[i].w

            // Check for large velocities
            translation POD b2Vec2 = h * v
            if b2Dot(translation, translation) > b2_maxTranslationSquared {
                ratio float = b2_maxTranslation / translation.Length()
                v.MultiplyAssign(ratio)
            }

            rotation float = h * w
            if rotation * rotation > b2_maxRotationSquared {
                ratio float = b2_maxRotation / b2Abs(rotation)
                w *= ratio
            }

            // Integrate
            c.AddAssign(h * v)
            a += h * w

            this.m_positions[i].c = c
            this.m_positions[i].a = a
            this.m_velocities[i].v = v
            this.m_velocities[i].w = w

            // Sync bodies
            body *b2Body = this.m_bodies[i]
            body.m_sweep.c = c
            body.m_sweep.a = a
            body.m_linearVelocity = v
            body.m_angularVelocity = w
            body.SynchronizeTransform()
        }

        this.Report(contactSolver.m_velocityConstraints)
    }

    func Add(inout body *b2Body) void {
        b2Assert(this.m_bodyCount < this.m_bodyCapacity, #get __file__, #get __line__, 'b2Assert(this.m_bodyCount < this.m_bodyCapacity)')
        body.m_islandIndex = this.m_bodyCount
        this.m_bodies[this.m_bodyCount] = body
        ++this.m_bodyCount
    }

    func Add(contact *b2Contact) void {
        b2Assert(this.m_contactCount < this.m_contactCapacity, #get __file__, #get __line__, 'b2Assert(this.m_contactCount < this.m_contactCapacity)')
        this.m_contacts[this.m_contactCount++] = contact
    }

    func Add(joint *b2Joint) void {
        b2Assert(this.m_jointCount < this.m_jointCapacity, #get __file__, #get __line__, 'b2Assert(this.m_jointCount < this.m_jointCapacity)')
        this.m_joints[this.m_jointCount++] = joint
    }

    func Report(constraints *b2ContactVelocityConstraint) void {
        if this.m_listener == null, return

        for(i int = 0; i < this.m_contactCount; ++i){
            c *b2Contact = this.m_contacts[i]

            vc *b2ContactVelocityConstraint = constraints at i
            
            impulse POD b2ContactImpulse = undef
            impulse.count = vc.pointCount
            
            for(j int = 0; j < vc.pointCount; ++j){
                impulse.normalImpulses[j] = vc.points[j].normalImpulse
                impulse.tangentImpulses[j] = vc.points[j].tangentImpulse
            }

            this.m_listener.PostSolve(c, &impulse)
        }
    }
}

func b2Island(uninitialized *b2Island, bodyCapacity, contactCapacity, jointCapacity int, allocator *b2StackAllocator, listener *b2ContactListener) *b2Island {
    uninitialized.m_bodyCapacity = bodyCapacity
    uninitialized.m_contactCapacity = contactCapacity
    uninitialized.m_jointCapacity = jointCapacity
    uninitialized.m_bodyCount = 0
    uninitialized.m_contactCount = 0
    uninitialized.m_jointCount = 0

    uninitialized.m_allocator = allocator
    uninitialized.m_listener = listener

    uninitialized.m_bodies = uninitialized.m_allocator.Allocate(bodyCapacity * sizeof *b2Body) as **b2Body
    uninitialized.m_contacts = uninitialized.m_allocator.Allocate(contactCapacity * sizeof *b2Contact) as **b2Contact
    uninitialized.m_joints = uninitialized.m_allocator.Allocate(jointCapacity * sizeof *b2Joint) as **b2Joint

    uninitialized.m_velocities = uninitialized.m_allocator.Allocate(uninitialized.m_bodyCapacity * sizeof b2Velocity) as *b2Velocity
    uninitialized.m_positions = uninitialized.m_allocator.Allocate(uninitialized.m_bodyCapacity * sizeof b2Position) as *b2Position
    return uninitialized
}
